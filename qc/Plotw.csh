#!/bin/csh -f
# Plots Eyrewell data; creates eyrplot.pdf in /amp/magobs/eyr/
# $1 gives sub-directory to get files from, $2 is label on graph
# Then writes web page

#  Directory for current magnetic input area for station $1  
cd /amp/magobs/$1

set year = `date -u '+%y'`
echo $year 
set yr = `date -u '+%Y'`
set mth = `date -u '+%m'`
set day = `date -u '+%d'`
set hr = `date  -u '+%H'`
set doyb = `date   --date='1 day ago' -u '+%j'`
set doye = `date   --date='+1 day' -u '+%j'`
echo $year $yr $mth $day $hr $doyb 

 set xscale = 8.0
 set yscale = 0.05
 set nlo = 19310
 set elo = 8210
 set ulo = -53580
 set flo = 57520

 set statmthps = $1'plot.ps'
 echo $statmthps
#EOF
#      N becomes X
#      E becomes Y
#      Z stays   Z
#      ZD becomes F


   set nhi =  `gawk -v x=$nlo 'BEGIN{printf("%9.2f",x+100.)}'`
   set nmid = `gawk -v x=$nlo 'BEGIN{printf("%9.2f",x+50.)}'`
   set ehi =  `gawk -v x=$elo 'BEGIN{printf("%9.2f",x+100.)}'`
   set emid = `gawk -v x=$elo 'BEGIN{printf("%9.2f",x+50.)}'`
   set uhi =  `gawk -v x=$ulo 'BEGIN{print x+100.}'`
   set umid = `gawk -v x=$ulo 'BEGIN{print x+50.}'`
   set fhi =  `gawk -v x=$flo 'BEGIN{printf("%9.2f",x+100.)}'`
   set fmid = `gawk -v x=$flo 'BEGIN{printf("%9.2f",x+50.)}'`
   set ftxt = `gawk -v x=$flo 'BEGIN{printf("%9.2f",x+09.)}'`  
#echo N $nlo $ntxt $nhi $nmid
#echo E $elo $ehi $emid
#echo U $ulo $uhi $umid

# Setup GMT parameters
   gmtset PAPER_MEDIA a4
   gmtset MEASURE_UNIT cm
   set PROJ = "-Jx"$xscale"/"$yscale
   set LIMITS = "-R"$doyb"/"$doye"/"$nlo"/"$nhi
   set FLAGS = "-P -V"
   set file = plot.ps
   psbasemap ${PROJ} ${LIMITS} ${FLAGS} -Ba1f1:"Eyrewell Magnetic Record":/a25f25WENs -K -V -X2.5 -Y20.5 > $statmthps

#   pstext  ${PROJ} ${LIMITS} ${FLAGS} -K  -O << EOF  >> $statmthps
#      $doyb.01 $nhi-10 18 0 4 LM  $2 
#EOF
  
# The file eyrplotfile.txt gets generated by HaveHour1w_N.csh and is written into /amp/magobs/eyr/ every hour at 22 minutes past. This is the file gawk is extracting values from:
   set filen = $1'plotfile.txt'
   set timestamp = `date`

# North Component
   set COLOUR = "-W1/250/0/0"			# red
   set label =  "X"
   rm -f temp_x
   gawk -v y=$yr '{print $3+substr($2,1,2)/24.+substr($2,4,2)/1440." " $4}' $filen >! temp_x
   psxy temp_x ${PROJ} ${LIMITS} ${FLAGS} ${COLOUR} -Sc0.05  -K -O      >> $statmthps
   pstext  ${PROJ} ${LIMITS} ${FLAGS}   -K -O << EOF  >> $statmthps
   $doyb.01 $nmid 18 0 4 LM $label 
EOF

# East Component
   set LIMITS = "-R"$doyb"/"$doye"/"$elo"/"$ehi
   psbasemap ${PROJ} ${LIMITS} ${FLAGS} -Bf1/a25f25WEsn -K -O -Y-5.5  >> $statmthps
   set COLOUR = "-W1/0/250/0"			# green
   set label =  "Y"
   rm -f temp_y
   gawk -v y=$yr '{print $3+substr($2,1,2)/24.+substr($2,4,2)/1440." " $5}' $filen >! temp_y
   psxy temp_y ${PROJ} ${LIMITS} ${FLAGS} ${COLOUR} -Sc0.05  -K -O      >> $statmthps
   pstext  ${PROJ} ${LIMITS} ${FLAGS}   -K -O << EOF  >> $statmthps
   $doyb.01 $emid 18 0 4 LM $label 
EOF
#  psxy ${PROJ} ${LIMITS} ${FLAGS} ${TCOLOUR} ${COLOUR} -Sc0.1  -K -O  << EOF  >> $statmthps
#$doyb.5 $emid 0.01 
#EOF

# Vertical Component
   set LIMITS = "-R"$doyb"/"$doye"/"$ulo"/"$uhi
   psbasemap ${PROJ} ${LIMITS} ${FLAGS} -Ba1f1/a25f25WEsn -K -O -Y-5.5  >> $statmthps
   set COLOUR = "-W1/0/0/250"			# blue
   set label =  "Z"
   rm -f temp_z
   gawk -v y=$yr '{print $3+substr($2,1,2)/24.+substr($2,4,2)/1440." " $6}' $filen >! temp_z
   psxy temp_z ${PROJ} ${LIMITS} ${FLAGS} ${COLOUR} -Sc0.05  -K -O      >> $statmthps
   pstext  ${PROJ} ${LIMITS} ${FLAGS}   -K -O << EOF  >> $statmthps
      $doyb.01 $umid 18 0 4 LM $label 
EOF

# Total Field (was Zenith Delay)
   set LIMITZ = "-R"$doyb"/"$doye"/"$flo"/"$fhi
   set Zlabel = "F"
   psbasemap ${PROJ} ${LIMITZ} ${FLAGS} -Ba1f1:"UT Day":/a25f25WESn -K -O -Y-5.5  >> $statmthps
   set COLOUR = "-W1/0/0/250"			# blue

#  Now plot total field F from the Overhauser magnetometer

   gawk -v y=$yr '{print $3+substr($2,1,2)/24.+substr($2,4,2)/1440." " $7}' $filen >! temp_f
   psxy temp_f ${PROJ} ${LIMITZ} ${FLAGS} -G0 -Sx0.05 -W1/0/250/250 -K -O >> $statmthps
   pstext  ${PROJ} ${LIMITZ} ${FLAGS}   -O << EOF  >> $statmthps
      $doyb.01 $fmid 18 0 4 LM $Zlabel 
      $doyb.07 $ftxt 14 0 4 LM $timestamp 
EOF

# Now plot the Summerhill Basalt
  
 

  echo Start File Conversion
   ps2pdfwr $statmthps $1'plot.pdf'
   echo End File Conversion
   rm /amp/ftp/pub/hurst/$1'plot.pdf'    
   cp $1'plot.pdf' /amp/ftp/pub/hurst
   echo End Web Service

echo `date`
